# A Snakemake pipeline for DADA2 processing of raw reads.
# The pipline filters and trims raw reads, identifies and annotates ASVs, and creates a phyloseq object.

configfile: "../config.yaml"

def config_(config_file, name):
    config_file_ = config_file['common']
    config_file_.update(config_file[name])
    return config_file_
    
config.update(config['common'])

module qc:
    # quality control
    snakefile: "0.2.1.quality_control.smk"
    config: config_(config, 'qc')
use rule * from qc as qc_*

module filter_trim:
    # DADA2: filter_and_trim
    snakefile: "0.2.2.filtering_trimming.smk"
    config: config_(config, 'filter_trim')
use rule * from filter_trim as filter_trim_*

module dada2:
    # DADA2 ASV detection and annotation and 
    # phyloseq object creation
    snakefile: "0.2.3.dada2.smk"
    config: config_(config, 'dada2')
use rule * from dada2 as dada2_*

rule all:
    # Request output of each module
    # For MultiQC two reports are requested: one performed based on the raw data, 
    # and another is after filtering and trimmning.
    input:
        expand(config['study_dir']+'QCcontrol/raw/multiqc/multiqc_report_{strand}.html', strand = config['qc']['strand']),
        rules.filter_trim_all.input,
        expand(config['study_dir']+'QCcontrol/filtered/multiqc/multiqc_report_{strand}.html', strand = config['qc']['strand']),
        rules.dada2_all.input
    default_target: True